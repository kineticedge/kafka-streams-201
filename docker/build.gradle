apply plugin: 'java'

// putting dependencies into the docker image to minimize startup time.
dependencies {
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: kafka_version
    implementation group: 'org.apache.kafka', name: 'kafka-streams', version: kafka_version
}

task extractDependencies(type: Copy) {
    from sourceSets.main.runtimeClasspath
    into 'build/runtime/'
}

task dockerBuild(type: Exec) {
    outputs.upToDateWhen {
        if (project.hasProperty('force-docker')) {
            return false
        } else {
            return true
        }
    }
    inputs.files 'Dockerfile'
    commandLine 'docker', 'build', '-t', "ks201_app:latest", '.'

    doFirst {
        exec {
            ignoreExitValue = true
            commandLine 'docker', 'tag', "ks201_app:latest", "ks201_app:prev"
        }
    }

    doLast {
        exec {
            ignoreExitValue = true
            commandLine 'docker', 'rmi', "ks201_app:prev"
        }
    }
}

build.finalizedBy extractDependencies
build.finalizedBy dockerBuild
